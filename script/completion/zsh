#compdef z1

_init() {
  typeset -A opt_args
  local context state line ret=1

  _arguments -C \
    '1: :_commands' \
    '*::arg:->args' \
    '-V[print version]' \
    '--version[print version]' \
  && ret=0

  case $state in
  (args)
    case $line[1] in
    (start)
      _arguments \
        '(-n --name)'{-n,--name}'[name of your app]' \
        '(-p --ports)'{-p,--ports}'[ports that your app listens to]' \
        '(-w --workers)'{-w,--workers}'[count of workers]' \
        '(-o --output)'{-o,--output}'[directory for the log files]' \
        '(-i --immediate)'{-i,--immediate}'[exit immediately]' \
        '::optional arg:{_path_files -/}' \
      && ret=0
      ;;
    (stop|restart)
      _arguments \
        '(-t --timeout)'{-t,--timeout}'[time until the workers get killed]' \
        '(-s --signal)'{-s,--signal}'[kill signal]' \
        '(-i --immediate)'{-i,--immediate}'[exit immediately]' \
        '::optional arg:_apps' \
      && ret=0
      ;;
    (restart-all)
      _arguments \
        '(-t --timeout)'{-t,--timeout}'[time until the workers get killed]' \
        '(-s --signal)'{-s,--signal}'[kill signal]' \
        '(-i --immediate)'{-i,--immediate}'[exit immediately]' \
      && ret=0
      ;;
    (info)
      _arguments \
        '--name[output the appname]' \
        '--dir[output the directory of the app]' \
        '--ports[output the ports that the app uses]' \
        '--pending[output the number of pending workers]' \
        '--available[output the number of available workers]' \
        '--killed[output the number of killed workers]' \
        '--revive-count[output how often the app has been revived]' \
        '::optional arg:_apps' \
      && ret=0
      ;;
    (logs)
      _arguments \
        '::optional arg:_apps' \
      && ret=0
      ;;
    (list)
      _arguments \
        '(-m --minimal)'{-m,--minimal}'[minimalistic list (easy to parse)]' \
      && ret=0
      ;;
    (install|uninstall)
      _arguments \
        '::optional arg:_features' \
      && ret=0
      ;;
    *)
      _message 'no more arguments'
      ;;
    esac
    ;;
  esac
}

_commands() {
  local cmds;
  cmds=(
    'start:start the app in the dir'
    'stop:stop an app'
    'restart:restart an app'
    'restart-all:restart all apps'
    'logs:show the output of an app'
    'info:detailed information about an app'
    'list:overview of all running apps'
    'exit:kill the z1 daemon'
    'resurrect:start all apps that were started before exit'
    'install:install additional features'
    'uninstall:uninstall features'
  )
  _describe -t cmds 'z1 commands' cmds
}

_apps() {
  local apps="$(z1 list -m)"
  if [[ -n "$apps" ]]
  then
    _values 'Running apps' $(echo $apps)
  fi
}

_features() {
  _values 'Available features' $(z1 install -m)
}

_init "$@"
